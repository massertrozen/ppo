apply plugin: 'com.android.application'

ext.versionMajor = 0
ext.versionMinor = 0
ext.versionPatch = 0
ext.minimumSdkVersion = 23

android {
    compileSdkVersion 28

    def getVersionName = { ->
        def version = new ByteArrayOutputStream()

        exec {
            commandLine 'git', 'describe'
            standardOutput = version
        }
        ext.versionMajor = Integer.valueOf(version.toString().trim().split('\\.')[0])
        ext.versionMinor = Integer.valueOf(version.toString().trim().split('\\.')[1].split("\\-")[0])
        ext.versionPatch = Integer.valueOf(version.toString().trim().split('\\.')[1].split("\\-")[1])

        return ext.versionMajor + "." + ext.versionMinor + "." + ext.versionPatch
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "PPOAPP-${variant.versionName}.apk"
        }
    }

    defaultConfig {
        applicationId "com.skywex.ppoapp"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion 28
        versionName getVersionName()
        versionCode generateVersionCode()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
